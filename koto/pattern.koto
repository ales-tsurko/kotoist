import koto
from test import assert, assert_eq, assert_near
export inf = from number import infinity

export as_iter = |value|
  if koto.type(value) == "Iterator"
    loop
      next = value.next()
      if next == () then break
      yield next
  else
    loop
      yield value

export csv_pattern = |pattern, num, file_name|
  f = io.create "$file_name.csv"
  f.write_line "x,y"
  for n in 0..num
    f.write_line "$n,${pattern.next()}"

export rrand = |min, max|
  return (random.number() * (max - min)) + min

number.rand = |self|
  return self * random.number()

number.rand2 = |self|
  return random.number() * self * 2 - self

export exprand = |lo, hi|
  return lo * ((hi / lo).ln() * random.number()).exp()

number.fold = |self, lo, hi|
  x = self - lo

  # avoid the divide if possible
  if self >= hi 
    self = hi + hi - self
    if self >= lo
      return self
  else if self < lo
    self = lo + lo - self
    if self < hi
      return self
  else
    return self

  if hi == lo
    return lo

  # ok do the divide
  range = hi - lo
  range2 = range + range
  c = x - range2 * (x / range2).floor()
  if c >= range
    c = range2 - c

  return c + lo

number.round = |self, quant|
  if quant == 0.0 then self else (self / quant + 0.5).floor() * quant

number.sign = |self|
  if self >= 0 then 1 else -1

number.wrap = |self, lo, hi|
    range = 0
    if self >= hi
      range = hi - lo
      self -= range
      if self < hi
        return self
    else if self < lo
      range = hi - lo
      self += range
      if self >= lo
        return self
    else
      return self

    if hi == lo
      return lo
    return self - range * ((self - lo) / range).floor()

list.scramble = |self|
  result = []
  li_cp = self.copy()
  loop
    if li_cp.is_empty() then return result
    index = (li_cp.size() * random.number()).floor()
    result.push(li_cp.remove(index))

list.windex = |self|
  r = random.number()
  sum = 0
  for n, item in self.iter().enumerate()
    sum += item
    if sum >= r
      return n

list.normalize_sum = |self|
  divisor = self.iter().sum()
  return self.iter().each(|x| x / divisor).to_list()

iterator.add = |self, other|
  if koto.type(other) == "Int" or koto.type(other) == "Float"
    return self.each(|x| x + other)
  else if koto.type(other) == "Iterator"
    return self.zip(other).each(|(a, b)| a + b)
  else
    return self

iterator.sub = |self, other|
  if koto.type(other) == "Int" or koto.type(other) == "Float"
    return self.each(|x| x - other)
  else if koto.type(other) == "Iterator"
    return self.zip(other).each(|(a, b)| a - b)
  else
    return self

iterator.mul = |self, other|
  if koto.type(other) == "Int" or koto.type(other) == "Float"
    return self.each(|x| x * other)
  else if koto.type(other) == "Iterator"
    return self.zip(other).each(|(a, b)| a * b)
  else
    return self

iterator.div = |self, other|
  if koto.type(other) == "Int" or koto.type(other) == "Float"
    return self.each(|x| x / other)
  else if koto.type(other) == "Iterator"
    return self.zip(other).each(|(a, b)| a / b)
  else
    return self

iterator.mod = |self, other|
  if koto.type(other) == "Int" or koto.type(other) == "Float"
    return self.each(|x| x % other)
  else if koto.type(other) == "Iterator"
    return self.zip(other).each(|(a, b)| a % b)
  else
    return self

export pbeta = |lo, hi, prob1, prob2, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if prob1 == () then prob1 = 1
  if prob2 == () then prob2 = 1
  if length == () then length = inf
  lo_iter = as_iter lo
  hi_iter = as_iter hi
  prob1_iter = as_iter prob1
  prob2_iter = as_iter prob2
  lo_val = ()
  hi_val = ()

  while length > 0
    rprob1 = prob1_iter.next()
    rprob2 = prob2_iter.next()

    if rprob1 == () or rprob2 == ()
      yield ()
      break

    rprob1 = rprob1.recip()
    rprob2 = rprob2.recip()

    lo_val = lo_iter.next()
    hi_val = hi_iter.next()

    if lo_val == () or hi_val == ()
      yield ()
      break

    sum = 2
    temp = 0

    while sum > 1
      temp = random.number().pow(rprob1)
      sum = temp + random.number().pow(rprob2)

    yield ((temp / sum) * (hi_val - lo_val)) + lo_val

    length -= 1

export pbrown = |lo, hi, step, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if step == () then step = 0.125
  if length == () then length = inf
  lo_iter = as_iter lo
  hi_iter = as_iter hi
  step_iter = as_iter step

  lo_val = lo_iter.next()
  hi_val = hi_iter.next()
  step_val = step_iter.next()

  if lo_val == () or hi_val == () or step_val == ()
    yield ()
    return

  cur = rrand lo_val, hi_val

  while length > 0
    lo_val = lo_iter.next()
    hi_val = hi_iter.next()
    step_val = step_iter.next()

    if lo_val == () or hi_val == () or step_val == ()
      yield ()
      break

    next = cur + step_val.rand2()
    cur = next.fold(lo_val, hi_val)

    yield cur

    length -= 1

export pgbrown = |lo, hi, step, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if step == () then step = 0.125
  if length == () then length = inf
  lo_iter = as_iter lo
  hi_iter = as_iter hi
  step_iter = as_iter step

  lo_val = lo_iter.next()
  hi_val = hi_iter.next()
  step_val = step_iter.next()

  if lo_val == () or hi_val == () or step_val == ()
    yield ()
    return

  cur = rrand lo_val, hi_val

  while length > 0
    lo_val = lo_iter.next()
    hi_val = hi_iter.next()
    step_val = step_iter.next()

    if lo_val == () or hi_val == () or step_val == ()
      yield ()
      break

    next = cur * (1 + step_val.rand2())
    cur = next.fold(lo_val, hi_val)

    yield cur

    length -= 1

export pcauchy = |mean, spread, length|
  if mean == () then mean = 0
  if spread == () then spread = 1
  if length == () then length = inf
  mean_iter = as_iter mean
  spread_iter = as_iter spread
  mean_val = ()
  spread_val = ()

  while length > 0
    ran = 0.5
    mean_val = mean_iter.next()
    spread_val = spread_iter.next()

    if mean_val == () or spread_val == ()
      yield ()
      break

    while ran == 0.5
      ran = random.number()

    yield ((ran * number.pi).tan() * spread_val) + mean_val

    length -= 1

export pclump = |n, pattern|
  li = []

  loop
    next = pattern.next()

    if next == ()
      yield li
      break

    li.push next

    if li.size() == n
      yield li
      li = []

export pclutch = |pattern, connected|
  if connected == () then connected = true
  val = ()

  loop
    is_con = connected.next()

    if is_con == ()
      yield ()
      break
    else if is_con
      val = pattern.next()
      yield val
      if val == ()
        break
    else
      yield val

export pconst = |sum, pattern, tollerance|
  if tollerance == () then tollerance = 0.001
  iter = as_iter pattern
  delta = ()
  elapsed = 0.0
  next_elapsed = ()

  loop 
    delta = iter.next()

    if delta == ()
      yield sum - elapsed
      break

    next_elapsed = elapsed + delta

    if next_elapsed.round(tollerance) >= sum
      yield sum - elapsed
      break
    else
      elapsed = next_elapsed
      yield delta

export pdup = |n, pattern|
  if koto.type(n) == "Iterator"
    loop
      times = n.next()
      if times == () then break
      val = pattern.next()
      if val == () then break
      while times > 0
        yield val
        times -= 1
  else 
    val = pattern.next()
    if val == () then return
    while n > 0
      yield val
      n -= 1

export pexprand = |lo, hi, length|
  if lo == () then lo = 0.0001
  if hi == () then hi = 1
  if length == () then length = inf
  lo_iter = as_iter lo
  hi_iter = as_iter hi

  while length > 0
    hi_val = hi_iter.next()
    lo_val = lo_iter.next()

    if hi_val == () or lo_val == () then break

    yield exprand(lo_val, hi_val)

    length -= 1

export pgauss = |mean, dev, length|
  if mean == () then mean = 0
  if dev == () then dev = 1
  if length == () then length = inf
  mean_iter = as_iter mean
  dev_iter = as_iter dev

  while length > 0
    dev_val = dev_iter.next()
    mean_val = mean_iter.next()

    if mean_val == () or dev_val == () then break

    a = (-2 * random.number().ln()).sqrt()
    b = (2 * number.pi * random.number()).sin()
    yield a * b * dev_val + mean_val

    length -= 1

export pgeom = |start, grow, length|
  if start == () then start = 0
  if grow == () then grow = 1
  if length == () then length = inf
  grow_iter = as_iter grow
  outval = ()
  counter = 0

  while counter < length
    grow_val = grow_iter.next()

    if grow_val == () then break

    outval = start
    start = start * grow_val
    counter += 1
    yield outval

export pwhite = |lo, hi, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if length == () then length = inf
  lo_iter = as_iter lo
  hi_iter = as_iter hi

  while length > 0
    hi_val = hi_iter.next()
    lo_val = lo_iter.next()

    if hi_val == () or lo_val == () then break

    length -= 1

    yield rrand lo_val, hi_val

export plprand = |lo, hi, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if length == () then length = inf
  iter1 = pwhite lo, hi, length
  iter2 = pwhite lo, hi, length
  while length > 0
    length -= 1

    yield iter1.next().min iter2.next()

export phprand = |lo, hi, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if length == () then length = inf
  iter1 = pwhite lo, hi, length
  iter2 = pwhite lo, hi, length
  while length > 0
    length -= 1

    yield iter1.next().max iter2.next()

export pmeanrand = |lo, hi, length|
  if lo == () then lo = 0
  if hi == () then hi = 1
  if length == () then length = inf
  iter1 = pwhite lo, hi, length
  iter2 = pwhite lo, hi, length
  while length > 0
    length -= 1

    yield (iter1.next() + iter2.next()) * 0.5

export pindex = |list_pat, index_pat, repeats|
  if repeats == () then repeats = 1
  index_iter = ()
  index = ()
  item = ()
  item_count = ()
  list_iter = as_iter list_pat
  while repeats > 0
    list = list_iter.next()
    if list == () then break
    index_iter = as_iter index_pat
    item_count = 0
    loop
      index = index_iter.next()
      if index == () then break
      item_count += 1
      item = list[index%list.size()]
      if koto.type(item) == "Iterator"
        iter = item.copy()
        loop
          next = iter.next()
          if next == () then break
          yield next
      else
        yield item
    if item_count == 0 then break
    repeats -= 1

export pfsm = |list, repeats|
  if list.is_empty() then return
  if repeats == () then repeats = 1
  item = ()
  index = 0
  max_state = ((list.size() - 1) / 2).floor() - 1
  while repeats > 0
    index = 0
    loop
      index = (random.pick list[index]).clamp(0, max_state) * 2 + 2
      item = list[index - 1]
      if item == () then break
      if koto.type(item) == "Iterator"
        iter = item.copy()
        loop
          next = iter.next()
          if next == () then break
          yield next
      else
        yield item
    repeats -= 1

export place = |list, repeats, offset|
  if repeats == () then repeats = 1
  if offset == () then offset = 0
  offset_iter = as_iter offset
  # we don't use repeats here as it can be inf, which won't work for
  # indexing
  repeat_num = 0 
  while repeats > 0
    offset = offset_iter.next()
    if offset == () then break
    for i in 0..list.size()
      item = list[(i + offset)%list.size()]
      if koto.type(item) == "List"
        item = item[repeat_num%item.size()]
      item = as_iter item
      yield item.next()
    repeat_num += 1
    repeats -= 1

export ppoisson = |mean, length|
  if mean == () then mean = 1
  if length == () then length = inf
  mean_iter = as_iter mean
  while length > 0
    mean_val = mean_iter.next()
    if mean_val == () then break
    inc = 0
    test = random.number()
    temp = (-1 * mean_val).exp()
    while test > temp
      inc += 1
      test *= random.number()
    yield inc
    length -= 1

export prand = |list, repeats|
  if repeats == () then repeats = 1
  while repeats > 0
    item = random.pick list
    if item == () then break
    if koto.type(item) == "Iterator"
      iter = item.copy()
      loop
        next = iter.next()
        if next == () then break
        yield next
    else
      yield item
    repeats -= 1

export prorate = |proportion, pattern|
  if pattern == () then pattern = 1
  iter = as_iter pattern
  prop = as_iter proportion
  loop
    val = iter.next()
    c = prop.next()
    if val == () or c == () then break
    if koto.type(c) == "List"
      for el in c
        yield el * val
    yield c * val
    yield (1 - c) * val

export pseq = |list, repeats, offset|
  if repeats == () then repeats = 1
  if offset == () then offset = 0
  offset = as_iter offset
  while repeats > 0
    offset_next = offset.next()
    if offset_next == () then break
    for n in 0..list.size()
      event = list[(n + offset_next)%list.size()]
      if koto.type(event) == "Iterator"
        event = event.copy()
        loop
          next = event.next()
          if next == () then break
          yield next
      else
        yield event
    repeats -= 1

export pser = |list, repeats, offset|
  if repeats == () then repeats = 1
  if offset == () then offset = 0
  offset = as_iter offset
  n = 0 # we use it, because repeats can be inf
  while repeats > 0
    offset_next = offset.next()
    if offset_next == () then break
    item = list[(offset_next + n)%list.size()]
    n += 1
    if koto.type(item) == "Iterator"
      iter = item.copy()
      loop
        next = iter.next()
        if next == () then break
        yield next
    else
      yield item

    repeats -= 1

export pseries = |start, step, length|
  if start == () then start = 0
  if step == () then step = 1
  if length == () then length = inf
  cur = as_iter start
  cur = cur.next()
  len = as_iter length
  len = len.next()
  step_iter = as_iter step
  while len > 0
    step_val = step_iter.next()
    if step_val == () then break
    outval = cur
    cur += step_val
    len -= 1
    yield outval

export pshuf = |list, repeats|
  if repeats == () then repeats = 1
  list = list.scramble()
  while repeats > 0
    for item in list
      if koto.type(item) == "Iterator"
        iter = item.copy()
        loop
          next = iter.next()
          if next == () then break
          yield next
      else
        yield item
    repeats -= 1

export pslide = |list, repeats, len, step, start, wrap_at_end|
  if repeats == () then repeats = 1
  if len == () then len = 3
  if step == () then step = 1
  if start == () then start = 0
  if wrap_at_end == () then wrap_at_end = true
  pos = start
  step_iter = as_iter step
  length_iter = as_iter len
  while repeats > 0
    length_val = length_iter.next()
    if length_val == () then return
    if wrap_at_end
      for n in 0..length_val
        item = list[(pos + n)%list.size()]
        if koto.type(item) == "Iterator"
          next = item.next()
          if next == () then break
          yield next
        else
          yield item
    else
      for n in 0..length_val
        item = list.get(pos + n)
        if item == () then return
        if koto.type(item) == "Iterator"
          next = item.next()
          if next == () then break
          yield next
        else
          yield item
    step_val = step_iter.next()
    if step_val == () then return
    pos += step_val
    repeats -= 1

export psubdivide = |n, pattern|
  durs = as_iter pattern
  subdivisions = as_iter n
  dur = durs.next()
  subdivision = subdivisions.next()
  while dur != () and subdivision != ()
    if subdivision > 0 # 0 skips it
      if subdivision > 1
        dur /= subdivision
        for _ in 0..subdivision
          yield dur
      else
        yield dur
    dur = durs.next()
    subdivision = subdivisions.next()

export pswitch = |list, which|
  if which == () then which = 0
  index_iter = as_iter which
  index = index_iter.next()
  while index != ()
    item = list[index.floor()%list.size()]
    if koto.type(item) == "Iterator"
      iter = item.copy()
      loop
        next = iter.next()
        if next == () then break
        yield next
    else
      yield item
    index = index_iter.next()

export ptuple = |list, repeats|
  if repeats == () then repeats = 1
  while repeats > 0
    saw_nil = false
    streams = list.iter().each(|i| as_iter i).to_list()
    while not saw_nil
      tuple = []
      for stream in streams
        outval = stream.next()
        if outval == ()
          saw_nil = true
          break
        tuple.push outval
      yield tuple
    repeats -= 1

export pwalk = |list, step_pattern, direction_pattern, start_pos|
  if direction_pattern == () then direction_pattern = 1
  if start_pos == () then start_pos = 0
  index = as_iter(start_pos).next()
  step_iter = as_iter step_pattern
  direction_iter = as_iter direction_pattern
  direction = direction_iter.next()
  if direction == () then direction = 1
  step = step_iter.next()
  while step != ()
    item = list[index]
    if item == () then break
    if koto.type(item) == "Iterator"
      iter = item.copy()
      loop
        next = iter.next()
        if next == () then break
        yield next
    else
      yield item
    step *= direction
    if (index + step) < 0 or (index + step) >= list.size()
      direction = direction_pattern.next()
      if direction == () then direction = 1
      step = step.abs() * direction.sign()
    index = (index + step) % list.size()
    step = step_iter.next()

export pwrand = |list, weights, repeats|
  if repeats == () then repeats = 1
  w_iter = as_iter weights
  while repeats > 0
    w_val = w_iter.next()
    if w_val == () then break
    item = list[w_val.windex()]
    if koto.type(item) == "Iterator"
      iter = item.copy()
      loop
        next = iter.next()
        if next == () then break
        yield next
    else
      yield item
    repeats -= 1

export pxrand = |list, repeats|
  if repeats == () then repeats = 1
  index = list.size().rand().floor()
  while repeats > 0
    size = list.size()
    index = (index + (size - 1).rand().floor() + 1) % size
    item = list[index]
    if koto.type(item) == "Iterator"
      iter = item.copy()
      loop
        next = iter.next()
        if next == () then break
        yield next
    else
      yield item
    repeats -= 1

pn = |pattern, repeats|
  while repeats > 0
    iter = pattern.copy()
    loop
      next = iter.next()
      if next == () then break
      yield next
    repeats -= 1


export @tests =

  @pre_test: ||
    random.seed 1

  @test as_iter: ||
    foo = as_iter 42
    bar = as_iter (0..=2).iter()

    for _ in 0..10
      assert_eq foo.next(), 42

    assert_eq bar.next(), 0
    assert_eq bar.next(), 1
    assert_eq bar.next(), 2
    assert_eq bar.next(), ()

  @test rrand: ||
    expected = [
      -1.2,
      3.469957416803331,
      -0.5,
      0.6902665517461255,
      -4.2,
      1.2452560333025064,
      -1.3,
      2.021086948798768,
      0.5944838253701086,
      -1.2,
      2.2390825654974087,
      -1.2,
      -0.6,
      -3.0,
      2.652270958284124,
      1.319258871135501,
      1.3631716977544475,
      2.5323785612426306,
      -3.5,
      -0.9
    ]
    for n in 0..20
      assert_near expected[n], rrand(5, -5), 0.1

  @test rand2: ||
    expected = [
      1.1870385830140222,
      -3.5,
      0.46718513136948303,
      -0.7,
      4.229941062594593,
      -1.2,
      1.3320208812485959,
      -2.0,
      -0.6,
      1.1719347584951194,
      -2.2,
      1.2042587403034766,
      0.6401493513957837,
      3.0197016659333222,
      -2.7,
      -1.3,
      -1.4,
      -2.5,
      3.4839803191568848,
      0.8963610833390785
    ]
    for item in expected
      assert_near 5.rand2(), item, 0.1

  @test fold: ||
    expected = [
      2.812961416985978,
      2.5300425831966695,
      2.467185131369483,
      2.6902665517461255,
      2.229941062594593,
      2.7547439666974936,
      2.667979118751404,
      2.021086948798768,
      2.5944838253701086,
      2.8280652415048806,
      2.2390825654974087,
      2.7957412596965234,
      2.6401493513957837,
      2.9802983340666778,
      2.6522709582841237,
      2.680741128864499,
      2.6368283022455525,
      2.5323785612426306,
      2.5160196808431152,
      2.8963610833390785,
    ]
    for item in expected
      assert_near 5.rand2().fold(2, 3), item, 0.000001

  @test round: ||
    assert_eq 4.5.round(0.5), 4.5
    assert_eq 0.01.round(0.5), 0
    assert_eq 0.9.round(0.5), 1.0

  @test number_wrap: ||
    assert_near 1.1.wrap(0.0, 1.0), 0.1, 0.1
    assert_near 1.1.wrap(0.0, -1.0), -0.9, 0.1
    assert_near 2.1.wrap(2.0, -1.0), -0.9, 0.1

  @test scramble: ||
    result = (0..10).to_list().scramble()
    expected = [6, 1, 5, 4, 9, 2, 7, 0, 3, 8]
    assert_eq result, expected

  @test windex: ||
    list = [0.1, 0.6, 0.3]
    result = []
    for _ in 0..100
      result.push list.windex()
    assert_eq result.iter().keep(|x| x == 0).count(), 9
    assert_eq result.iter().keep(|x| x == 1).count(), 71
    assert_eq result.iter().keep(|x| x == 2).count(), 20

    list = [0.25, 0.25, 0.25, 0.25]
    result = []
    for _ in 0..100
      result.push list.windex()
    assert_eq result.iter().keep(|x| x == 0).count(), 21
    assert_eq result.iter().keep(|x| x == 1).count(), 29
    assert_eq result.iter().keep(|x| x == 2).count(), 22
    assert_eq result.iter().keep(|x| x == 3).count(), 28

  @test normalize_sum: ||
    list = [1,3,10]
    result = list.normalize_sum()
    expected = [0.07142857142857142, 0.21428571428571427, 0.7142857142857143]
    for n in 0..expected.size()
      assert_near result[n], expected[n], 0.00001

  @test iterator_add: ||
    iter = pseq([1,2,3]).add(12)
    expected = [13,14,15]
    for item in expected
      assert_eq iter.next(), item
    iter = pseq([1,2,3], inf).add(pseq([1,2], inf))
    expected = [2,4,4,3,3,5]
    for item in expected
      assert_eq iter.next(), item

  @test iterator_sub: ||
    iter = pseq([1,2,3]).sub(12)
    expected = [-11, -10, -9]
    for item in expected
      assert_eq iter.next(), item
    iter = pseq([1,2,3], inf).sub(pseq([1,2], inf))
    expected = [0,0,2,-1,1,1]
    for item in expected
      assert_eq iter.next(), item

  @test iterator_mul: ||
    iter = pseq([1,2,3]).mul(12)
    expected = [12, 24, 36]
    for item in expected
      assert_eq iter.next(), item
    iter = pseq([1,2,3], inf).mul(pseq([1,2], inf))
    expected = [1,4,3,2,2,6]
    for item in expected
      assert_eq iter.next(), item

  @test iterator_div: ||
    iter = pseq([2, 4, 6]).div(2)
    expected = [1,2,3]
    for item in expected
      assert_eq iter.next(), item
    iter = pseq([2,4,6], inf).div(pseq([1,2], inf))
    expected = [2,2,6,1,4,3]
    for item in expected
      assert_eq iter.next(), item

  @test iterator_mod: ||
    iter = pseq([2, 3, 4]).mod(2)
    expected = [0,1,0]
    for item in expected
      assert_eq iter.next(), item
    iter = pseq([2,4,6], inf).mod(pseq([2,3], inf))
    expected = [0, 1, 0, 2, 0, 0]
    for item in expected
      assert_eq iter.next(), item

  @test pbeta: ||
    iter = pbeta 0, 1, 0.1, 0.1
    expected = [
      0.9999991445264916,
      0.9152024196110354,
      0.999875903336608,
      0.9994692080756311,
      0.03319654811950952,
      0.0003044408621467811,
      0.028751099344361247,
      0.011023335524190322,
      0.9797386852643469,
      0.9743803780361755,
      0.3407028163560437,
      0.9999999999999301,
      0.7631398280331377,
      0.9946581942463375,
      0.9816463243418938,
      0.25330909537521223,
      0.9999999221592342,
      0.9999148879416475,
      0.8848777651792713,
      0.9272571307795654
    ]

    for item in expected
      assert_near iter.next(), item, 0.000001

  @test pbrown: ||
    iter = pbrown 0, 1, 0.1
    expected = [
      0.5493047099653355,
      0.5586484125927252,
      0.5448430815578027,
      0.6294419028096946,
      0.6045367821436445,
      0.6311771997686164,
      0.5907554607926411,
      0.5788657842852389,
      0.6023044794551413,
      0.5575228281451932,
      0.5816080029512627,
      0.5944109899791784,
      0.6548050232978448,
      0.6017596041321623,
      0.5753744267094523,
      0.5481109927543634,
      0.49746342152951073,
      0.5671430279126484,
      0.58507024957943,
      0.5621729750717894,
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test pgbrown: ||
    iter = pgbrown 0, 1, 0.2
    expected = [
      0.5328288166246906,
      0.5427860046523808,
      0.5277993237016826,
      0.6171017249870987,
      0.5863637391390359,
      0.6176056889206439,
      0.5676762970253886,
      0.5541773219602849,
      0.5801557085992849,
      0.5281950473033468,
      0.5536383873953493,
      0.567814837579307,
      0.6364000940185026,
      0.5688838745299202,
      0.5388636706051388,
      0.5094811224164588,
      0.4578731595458479,
      0.5216820026069645,
      0.5403866204075757,
      0.5156398588321188
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test pcauchy: ||
    iter = pcauchy 0, 10
    expected = [
      -25.6,
      5.2147170741223885,
      -67.6,
      45.38893160257427,
      -2.5,
      24.244271410115708,
      -22.5,
      13.573803034087756,
      52.91991583580018,
      -25.9,
      11.790140905744481,
      -25.2,
      -49.1,
      -7.2,
      9.086266805541115,
      22.730323458806293,
      21.905407476147406,
      9.798601187194997,
      -5.2,
      -34.6,
    ]

    for item in expected
      assert_near iter.next(), item, 0.1

  @test pclump: ||
    iter = pclump 2, pseq([1,2,3])
    expected = [[1,2], [3], ()]
    for item in expected
      assert_eq iter.next(), item

  @test pclutch: ||
    iter = pclutch pseq([0,1,2,3], 2), pseq([true, true, false], 3)
    expected = [0, 1, 1, 2, 3, 3, 0, 1, 1, ()]
    for item in expected
      assert_eq iter.next(), item

  @test pconst: ||
    iter = pconst 5, pseq([1,2,0.5,0.1], 2)
    expected = [1, 2, 0.5, 0.1, 1, 0.40000000000000036, ()]
    for item in expected
      assert_eq iter.next(), item

  @test pdup: ||
    iter = pdup 5, pseq([42,27], inf)
    expected = [42,42,42,42,42]

    for item in expected
      assert_eq iter.next(), item

    iter = pdup pseq([2,3,4],2), pseq([42,27], inf)
    expected = [
      42, 42, 27, 27, 27, 42, 42, 42, 42, 27, 27, 42, 42, 42, 27, 27, 27, 27
    ]

    for item in expected
      assert_eq iter.next(), item

  @test pexprand: ||
    iter = pexprand()
    expected = [
      0.029841141583300028,
      0.0004092767114825923,
      0.01537708632863361,
      0.005295334257655042,
      0.49201282684887976,
      0.003176125003187975,
      0.03410423816335522,
      0.001554408707669678,
      0.005783709479337844,
      0.029428891472169295,
      0.0012716481793197704,
      0.030318205764975875,
      0.018032657769266016,
      0.1613915031052206,
      0.0008691437603582264,
      0.0029668558888765978,
      0.002849255012017592,
      0.0009706185241079233,
      0.24750964522715005,
      0.022832025106861588
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test pgauss: ||
    iter = pgauss 0, 100
    expected = [
      80.35082837787117,
      46.18109205370832,
      28.22316915650585,
      91.30418859390468,
      -86.0,
      -110.1,
      -101.4,
      125.5030087331761,
      142.20088365392425,
      -30.6,
      72.64218281513229,
      4.087489843263244,
      -61.9,
      135.99953704404055,
      -4.8,
      -90.6,
      61.365490625579135,
      46.95618650843188,
      -48.5,
      44.024650157204086
    ]
    for item in expected
      assert_near iter.next(), item, 0.1

  @test pgeom: ||
    iter = pgeom 1, 1.1
    expected = [
      1,
      1.1,
      1.2100000000000002,
      1.3310000000000004,
      1.4641000000000006,
      1.6105100000000008,
      1.771561000000001,
      1.9487171000000014,
      2.1435888100000016,
      2.357947691000002,
      2.5937424601000023,
      2.853116706110003,
      3.1384283767210035,
      3.4522712143931042,
      3.797498335832415,
      4.177248169415656,
      4.594972986357222,
      5.054470284992944,
      5.559917313492239,
      6.115909044841463
    ]
    for item in expected
      assert_eq iter.next(), item

  @test pwhite: ||
    iter = pwhite()
    expected = [
      0.6187038583014022,
      0.1530042583196669,
      0.5467185131369483,
      0.4309733448253874,
      0.9229941062594592,
      0.37547439666974936,
      0.6332020881248596,
      0.2978913051201232,
      0.4405516174629891,
      0.6171934758495119,
      0.2760917434502591,
      0.6204258740303477,
      0.5640149351395783,
      0.8019701665933322,
      0.23477290417158758,
      0.36807411288644987,
      0.3636828302245553,
      0.24676214387573692,
      0.8483980319156884,
      0.5896361083339079
    ]
    for item in expected
      assert_near iter.next(), item, 0.0001

  @test plprand: ||
    iter = plprand()
    expected = [
      0.1530042583196669,
      0.4309733448253874,
      0.37547439666974936,
      0.2978913051201232,
      0.4405516174629891,
      0.2760917434502591,
      0.5640149351395783,
      0.23477290417158758,
      0.24676214387573692,
      0.5896361083339079,
      0.3855136274617973,
      0.04790208269762819,
      0.6365466664018155,
      0.15710685061085117,
      0.5102535624318508,
      0.6151173564933541,
      0.0712616454275049,
      0.33686989631048336,
      0.722317435472697,
      0.43311677419267547
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test phprand: ||
    iter = phprand()
    expected = [
      0.6187038583014022,
      0.5467185131369483,
      0.9229941062594592,
      0.6332020881248596,
      0.6171934758495119,
      0.6204258740303477,
      0.8019701665933322,
      0.36807411288644987,
      0.3636828302245553,
      0.8483980319156884,
      0.41182270242523855,
      0.9905396731814841,
      0.7155524219707493,
      0.2649681662161876,
      0.7596424830971644,
      0.685341434428002,
      0.3662141599669775,
      0.8599226673317435,
      0.8857282675143859,
      0.55865794478581
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test pmeanrand: ||
    iter = pmeanrand()
    expected = [
      0.38585405831053454,
      0.48884592898116785,
      0.6492342514646043,
      0.4655466966224914,
      0.5288725466562505,
      0.4482588087403034,
      0.6829925508664553,
      0.3014235085290187,
      0.3052224870501461,
      0.7190170701247982,
      0.3986681649435179,
      0.5192208779395562,
      0.6760495441862824,
      0.21103750841351937,
      0.6349480227645077,
      0.650229395460678,
      0.2187379026972412,
      0.5983962818211135,
      0.8040228514935415,
      0.49588735948924273
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test pindex: ||
    iter = pindex(
      [7, 13, 12, 2, 2, 2, 5], 
      pseq([0, 0, 2, 0, 4, 6, 7], inf), 
      inf)
    expected = [7, 7, 12, 7, 2, 5, 7, 7, 7, 12, 7, 2, 5, 7, 7, 7, 12, 7, 2, 5]
    for item in expected
      assert_eq iter.next(), item

  @test pfsm: ||
    iter = pfsm([
        [0,1],
        67, [0, 0, 3],
        72, [2],
        73, [0, 2],
        pseq([74, 75, 76, 77]), [2, 3, 3],
        (), ()], inf)
    expected = [72,73,73,73,73,73,73,67,67,67,67,74,75,76,77,74,75,76,77,74]
    for item in expected
      assert_eq iter.next(), item

  @test place: ||
    iter = place [1, [2, 5], [3, 6]], inf
    expected = [1,2,3,1,5,6,1,2]
    for item in expected
      assert_eq iter.next(), item

    iter = place(
      [1, pseq([2, 5], inf), pseq([3, 6], inf)], inf)
    for item in expected
      assert_eq iter.next(), item

  @test ppoisson: ||
    iter = ppoisson 10
    expected = [12,10,11,10,11,8,9,10,7,10,11,10,9,14,11,8,9,8,12,6]
    for item in expected
      assert_eq iter.next(), item

  @test prand: ||
    iter = prand [
      pseq([0,1]), 
      pseq([2,3]), 
      pseq([4,5])
    ], inf
    expected = [ 0, 1, 2, 3, 2, 3, 2, 3, 2, 3, 2, 3, 4, 5, 0, 1, 2, 3, 2, 3 ]
    for item in expected
      assert_eq iter.next(), item

    iter = prand (0..10).to_list(), inf
    expected = [ 2, 8, 4, 0, 7, 6, 1, 7, 5, 6, 8, 3, 7, 5, 4, 9, 1, 3, 2, 0 ]
    for item in expected
      assert_eq iter.next(), item

  @test prorate: ||
    iter = prorate pseq([0.35, 0.5, 0.8])
    expected = [0.35, 0.65, 0.5, 0.5, 0.8, 0.19999999999999996]
    for item in expected
      assert_near iter.next(), item, 0.001

    iter = prorate pseq([0.35, 0.5, 0.8]), prand([20, 1], inf)
    expected = [0.35, 0.65, 10.0, 10.0, 0.8, 0.19999999999999996]
    for item in expected
      assert_near iter.next(), item, 0.001

  @test pseq: ||
    foo = pseq [0,1,2], inf
    expected = [0,1,2,0,1]
    for item in expected
      assert_eq foo.next(), item

    foo = pseq [pseq([0,2], 2), pseq([1,3], 2)], 2
    expected = [0,2,0,2,1,3,1,3]
    for _ in 0..2
      for item in expected
        assert_eq foo.next(), item

    foo = pseq [0,1,2], inf, (pseq [0,1,4])
    expected = [0,1,2,1,2,0,1,2,0,()]
    for item in expected
      assert_eq foo.next(), item

  @test pser: ||
    iter = pser [1, 2, 3], 5
    expected = [1, 2, 3, 1, 2, ()]
    for item in expected
      assert_eq iter.next(), item

  @test pseries: ||
    iter = pseries 300, 20, 20
    expected = [
      300,
      320,
      340,
      360,
      380,
      400,
      420,
      440,
      460,
      480,
      500,
      520,
      540,
      560,
      580,
      600,
      620,
      640,
      660,
      680
    ]
    for item in expected
      assert_eq iter.next(), item

  @test pshuf: ||
    iter = pshuf (1..5).to_list(), 3
    expected = [3,1,4,2,3,1,4,2,3,1,4,2,()]
    for item in expected
      assert_eq iter.next(), item

  @test pslide: ||
    iter = pslide [1, 2, 3, 4, 5], inf
    expected = [1, 2, 3, 2, 3, 4, 3, 4, 5, 4, 5, 1, 5, 1, 2, 1, 2, 3, 2, 3]
    for item in expected
      assert_eq iter.next(), item

  @test psubdivide: ||
    iter = psubdivide(
      pseq([1,1,1,1,1,2,2,2,2,2,0,1,3,4,0], inf),
      pseq([0.5, 1, 2, 0.25,0.25],inf)
    )
    expected = [
      0.5,
      1,
      2,
      0.25,
      0.25,
      0.25,
      0.25,
      0.5,
      0.5,
      1.0,
      1.0,
      0.125,
      0.125,
      0.125,
      0.125,
      1,
      0.6666666666666666,
      0.6666666666666666,
      0.6666666666666666,
      0.0625
    ]
    for item in expected
      assert_near iter.next(), item, 0.00001

  @test pswitch: ||
    a = pseq [1, 2, 3], 2
    b = pseq [65, 76]
    iter = pswitch [a, b, 800], pseq([2, 2, 0, 1], inf)
    expected = [800,800,1,2,3,1,2,3,65,76,800,800,1,2,3,1,2,3,65,76]
    for item in expected
      assert_eq iter.next(), item

  @test ptuple: ||
    a = pseq [1, 2, 3], inf
    b = pseq [65, 76], inf
    iter = ptuple [a, a.copy(), b], inf
    expected = [
      [1, 1, 65],
      [2, 2, 76],
      [3, 3, 65],
      [1, 1, 76],
      [2, 2, 65],
      [3, 3, 76],
      [1, 1, 65],
      [2, 2, 76]
    ]
    for item in expected
      assert_eq iter.next(), item

  @test pwalk: ||
    iter = pwalk(
      [1, 2, 3, 4, 5, 6, 7], 
      pseq([1], inf), 
      pseq([1, -1], inf) 
    )
    expected = [1, 2, 3, 4, 5, 6, 7, 6, 5, 4, 3, 2, 1, 2, 3, 4, 5, 6, 7, 6]
    for item in expected
      assert_eq iter.next(), item

  @test pwrand: ||
    iter = pwrand([1, 2, 3], [1,3,5].normalize_sum(), inf)
    result = []
    for _ in 0..100
      result.push iter.next()
    assert_eq result.iter().take(100).keep(|x| x == 1).count(), 11
    assert_eq result.iter().take(100).keep(|x| x == 2).count(), 42
    assert_eq result.iter().take(100).keep(|x| x == 3).count(), 47

  @test pxrand: ||
    iter = pxrand [1, 2, 3], 10
    expected = [3,2,3,2,3,2,3,1,3,1,()]
    for item in expected
      assert_eq iter.next(), item

  @test pn: ||
    iter = pn pseq([1,2,3]), 2
    expected = [1,2,3,1,2,3,()]
    for item in expected
      assert_eq iter.next(), item
